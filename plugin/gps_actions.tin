#class gps_actions open;

#al {gps.clear} {
    #var clear_target %0;
    #var clear_menpai "";
    #replace clear_target {@} {;};
    #list clear_target_list create ${clear_target};
    #list clear_target_list size clear_target_size;
    #var clear_target $clear_target_list[1];
    #if {${clear_target_size} == 2} {
        #var clear_menpai $clear_target_list[2];
    };

    #if {"${char.menpai}" == "${clear_menpai}"} {
        gps.run;
        #return;
    };
    
    clear ${clear_target};
    chain on_cleared health gps.run;
};

#al {gps.delay} {
    #var gps_delay 1.5;
    #var gps_delay %1;
    #delay {gps_delay} {
        gps.run;
    } {${gps_delay}};
};

#al {gps.zuo_mufa} {
    #class zuo_mufa open;
    #ti {mufa} {
        zuo mufa;
    } {3};
    #ac {一只木筏从海上漂了过来。} {
        enter;
        #unticker {mufa};
    };
    #ac {木筏一震，搁浅了。显然是到陆地了，赶紧上去吧。} {
        #class zuo_mufa kill;
        out;
        #delay {gps_delay} {
            gps.run;            
        };
    };
    #class zuo_mufa close;
};

#al {gps.longgong} {
    #class longgong open;
    #ac {GOT_BISHUIZHOU} {
        #delay {gps_longgong_delay} {
            #class longgong kill;
            #delay {1} {
                gps.retry;
            };
        } {5};
        gt 东海之滨;
        on_there {
            dive;
        };
    };
    #ac {海底 -} {
        #undelay {gps_longgong_delay};
        #class longgong kill;
        #delay {1} {
            gps.retry;
        };
    };
    #class longgong close;
    
    get_bishuizhou;
};

#al {gps.honglou} {
    #class honglou open;
    #ac {不一会儿，你就进入了梦乡。} {
        #delay {gps_honglou_delay} {
            #class honglou kill;
            gps.retry;
        } {20};
    };
    #ac {GOT_PILLOW} {
        gt ${sleep_pos};
        on_there {
            sleep;
        };
    };
    #ac {荡悠悠三更梦 -} {
        #undelay {gps_honglou_delay};
        #class honglou kill;
        gps.retry;
    };
    #class honglou close;
    get_pillow;
};

#al {gps.hongloub} {
    #class hongloub open;
    #ac {你揉揉眼睛，醒了过来。} {
        #class hongloub kill;
        gps.retry;
    };
    #class hongloub close;
    ask girl about back;
};

#al {gps.fly} {
    #class gps_fly open;
    #ac {到了！你按下云头跳了下来。} {
        #class gps_fly kill;
        gps.retry;
    };
    #class gps_fly close;
    fly %1;
};
#al {gps.bgz} {
    #2 ne;
    #2 nw;
    #2 sw;
    se;n;
    gps.run;
};

#al {gps.yuntouqiao_n} {
    #al {yuntouqiao_check_exits} {
        #list yuntouqiao_exits create ${pos.exits};
        #if {@ismember{north;yuntouqiao_exits} != 0} {
            #send {north};
            gps.run;
        };
        #else {
            #echo 过不去...等桥降下来;
            #ac {慢慢聚在一起} {
                #unac {慢慢聚在一起};
                #send {north};
                gps.run;
            };
        };
    };
    get_pos {
        yuntouqiao_check_exits;
    };
};

#al {gps.yuntouqiao_s} {
    #al {yuntouqiao_check_exits} {
        #list yuntouqiao_exits create ${pos.exits};
        #if {@ismember{south;yuntouqiao_exits} != 0} {
            #send {south};
            gps.run;
        };
        #else {
            #echo 过不去...等桥降下来;
            #ac {慢慢聚在一起} {
                #unac {慢慢聚在一起};
                #send {south};
                gps.run;
            };
        };
    };
    get_pos {
        yuntouqiao_check_exits;
    };
};

#al {gps.xiezhidong} {
    #send {northeast};
    #ac {慢慢地你终于又有了知觉．．．} {
        #unac {慢慢地你终于又有了知觉．．．};
        gps.run;
    };
};

#al {gps.baigujing} {
    #class gps_baigujing open;
    #al {baigujing_clear_next} {
        #list baigujing_targets get ${baigujing_index} baigujing_target;
        clear ${baigujing_target};
    };
    
    #ac {CLEAR_DONE} {
        #math baigujing_index ${baigujing_index}+1;
        #if {${baigujing_index} >= 4} {
            #class gps_baigujing kill;
            southwest;
            health {
                gps.run;    
            };
        };
        #else {
            #delay {3} {
                baigujing_clear_next;    
            };
        };
    };
    #class gps_baigujing close;
    #list baigujing_targets create {nu;fu;gong};
    #var baigujing_index 1;
    baigujing_clear_next;
};

#al {gps.penglai} {
    get_penglai {
        fly penglai;
        gps.run;
    };
};

#al {gps.xueshan} {
    get_xueshan {
        fly xueshan;
        gps.run;
    };
};

#al {gps.cross_river} {
    #class cross_river open;
    #ac {你淋漓漓地爬上了岸。} {
        #class cross_river kill;
        gps.run;
    };
    #class cross_river close;
    cross river;
};

#al {gps.wudidong} {
    #class gps_wudidong open;
    #ti {push_statue} {
        jiali max; push statue;
    } {6};

    #ti {heal} {
        yun heal;
    } {2};
    #ac {你纵身一跃，跳了进去，大洞随即又合上了。} {
        #class gps_wudidong kill;
        #unticker {push_statue};
        #unticker {heal};
        gps.run;
    };
    #class gps_wudidong close;
    jiali max; push statue;
};

#al {gps.baiyundong} {
    gps.run_maze {north;north;north;northeast;northeast;northwest;north;north};
};

#al {gps.baihuagu} {
    gps.run_maze {
        north;west;south;south;east;east;north;north;north;west;
        west;west;south;south;south;south;east;east;east;east;north;north;north;north;
        {fly penglai};
    };
};

#al {gps.cliff} {
    #class gps_cliff open;
    #ti {climb} {
        climb cliff;
    } {10};
    #ac {你慢慢的爬了上来。} {
        #unticker {climb};
        #class gps_cliff kill;
        gps.run;
    };
    #class gps_cliff close;
    climb cliff;
};

#al {gps.zizhulin} {
    gps.run_maze {northwest;northeast;southwest;northwest;southeast};
};

#al {gps.madao} {
    gps.clear dao;
};
#class gps_actions close;
