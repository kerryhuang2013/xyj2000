#class gps_actions open;

#alias {gps.clear} {
    clear %0;
    on_cleared {
        gps.run;
    };
};

#alias {gps.delay} {
    #var gps_delay 1.5;
    #var gps_delay %1;
    #delay {gps_delay} {
        gps.run;
    } {${gps_delay}};
};

#alias {gps.zuo_mufa} {
    #class zuo_mufa open;
    #ticker {mufa} {
        zuo mufa;
    } {3};
    #action {一只木筏从海上漂了过来。} {
        enter;
        #unticker {mufa};
    };
    #action {木筏一震，搁浅了。显然是到陆地了，赶紧上去吧。} {
        #class zuo_mufa kill;
        out;
        #delay {gps_delay} {
            gps.run;            
        };
    };
    #class zuo_mufa close;
};

#alias {gps.longgong} {
    #class longgong open;
    #action {GOT_BISHUIZHOU} {
        #delay {gps_longgong_delay} {
            #class longgong kill;
            #delay {1} {
                gps.retry;
            };
        } {5};
        gt 东海之滨;
        on_there {
            dive;
        };
    };
    #action {海底 -} {
        #undelay {gps_longgong_delay};
        #class longgong kill;
        #delay {1} {
            gps.retry;
        };
    };
    #class longgong close;
    
    get_bishuizhou;
};

#alias {gps.honglou} {
    #class honglou open;
    #action {不一会儿，你就进入了梦乡。} {
        #delay {gps_honglou_delay} {
            #class honglou kill;
            gps.retry;
        } {20};
    };
    #action {GOT_PILLOW} {
        gt ${sleep_pos};
        on_there {
            sleep;
        };
    };
    #action {荡悠悠三更梦 -} {
        #undelay {gps_honglou_delay};
        #class honglou kill;
        gps.retry;
    };
    #class honglou close;
    get_pillow;
};

#alias {gps.hongloub} {
    #class hongloub open;
    #action {你揉揉眼睛，醒了过来。} {
        #class hongloub kill;
        gps.retry;
    };
    #class hongloub close;
    ask girl about back;
};

#alias {gps.fly} {
    #class gps_fly open;
    #action {到了！你按下云头跳了下来。} {
        #class gps_fly kill;
        gps.retry;
    };
    #class gps_fly close;
    fly %1;
};
#alias {gps.bgz} {
    #2 ne;
    #2 nw;
    #2 sw;
    se;n;
    gps.run;
};

#alias {gps.yuntouqiao_n} {
    #alias {yuntouqiao_check_exits} {
        #list yuntouqiao_exits create ${pos.exits};
        #if {@ismember{north;yuntouqiao_exits} != 0} {
            #send {north};
            gps.run;
        };
        #else {
            #echo 过不去...等桥降下来;
            #action {慢慢聚在一起} {
                #unaction {慢慢聚在一起};
                #send {north};
                gps.run;
            };
        };
    };
    get_pos {
        yuntouqiao_check_exits;
    };
};

#alias {gps.yuntouqiao_s} {
    #alias {yuntouqiao_check_exits} {
        #list yuntouqiao_exits create ${pos.exits};
        #if {@ismember{south;yuntouqiao_exits} != 0} {
            #send {south};
            gps.run;
        };
        #else {
            #echo 过不去...等桥降下来;
            #action {慢慢聚在一起} {
                #unaction {慢慢聚在一起};
                #send {south};
                gps.run;
            };
        };
    };
    get_pos {
        yuntouqiao_check_exits;
    };
};

#alias {gps.xiezhidong} {
    #send {northeast};
    #action {慢慢地你终于又有了知觉．．．} {
        #unaction {慢慢地你终于又有了知觉．．．};
        gps.run;
    };
};

#alias {gps.baidujing} {
    #class gps_baigujing open;
    #alais {baigujing_clear_next} {
        #list baigujing_targets get ${baigujing_index} baigujing_target;
        clear ${baigujing_target};
    };
    
    #action {CLEAR_DONE} {
        #math baigujing_index ${baigujing_index}+1;
        #if {${baigujing_index} >= 4} {
            #class gps_baigujing kill;
            gps.run;
        };
        #else {
            baigujing_clear_next;
        };
    };
    #class gps_baigujing close;
    #list baigujing_targets create {nu;fu;gong};
    #var baigujing_index 1;
    baigujing_clear_next;
};

#alias {gps.penglai} {
    get_penglai {
        fly penglai;
        gps.run;
    };
};

#alias {gps.cross_river} {
    #class cross_river open;
    #action {你淋漓漓地爬上了岸。} {
        #class cross_river kill;
        gps.run;
    };
    #class cross_river close;
    cross river;
};

#alias {gps.wudidong} {
    #class gps_wudidong open;
    #ticker {push_statue} {
        jiali max; push statue;
    } {6};

    #ticker {heal} {
        yun heal;
    } {2};
    #action {你纵身一跃，跳了进去，大洞随即又合上了。} {
        #class gps_wudidong kill;
        #unticker {push_statue};
        #unticker {heal};
        gps.run;
    };
    #class gps_wudidong close;
    jiali max; push statue;
};

#alias {gps.baiyundong} {
    gps.run_maze {north;north;north;northeast;northeast;northwest;north;north};
};

#alias {gps.cliff} {
    #class gps_cliff open;
    #ticker {climb} {
        climb cliff;
    } {10};
    #action {你慢慢的爬了上来。} {
        #unticker {climb};
        #class gps_cliff kill;
        gps.run;
    };
    #class gps_cliff close;
    climb cliff;
};
#class gps_actions close;
