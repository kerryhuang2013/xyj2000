#class picker open;
#list picker_items create {Silver armor;Iron armor;Pink skirt;Hammer;Gang cha;Teng jia};

#al {picker.start} {
    picker.setup;
    #var picker_item_index 1;
    #list picker_items size picker_item_size;
    #list picker_items get ${picker_item_index} picker_item;
    picker.bl;
};

#al {picker.bl} {
    gt 龙宫;
    on_there {
        bl.start {${picker_item}};
    };
};

#al {picker.stop} {
    #class picker kill;
    #undelay {picker_delay};
};

#al {picker.setup} {
    #class picker open;
    #ac {GPS: 遍历完成} {
        #math picker_item_index ${picker_item_index}+1;
        #if {${picker_item_index} > ${picker_item_size}} {
            #var picker_item_index 1;
        };
        gt 董记当铺;
        on_there {
            picker.sell_all @lower{${picker_item}};
        };
        
    };
    #ac {找到目标} {
        picker.get_all @lower{${picker_item}};
    };

    #ac {GET_ALL_DONE} {
        bl.continue;
    };

    #ac {GET_ALL_FULL} {
        gt 董记当铺;
        on_there {
            picker.sell_all @lower{${picker_item}};
        };
    };

    #ac {SELL_ALL_DONE} {
        #list picker_items get ${picker_item_index} picker_item;
        picker.bl;
    };
    #class picker close;
};

#al {picker.get_all} {
    #var get_all_item %0;
    #class get_all open;
    #ti {get_all} {
        get ${get_all_item};
    } {2};
    #ac {对你而言太重了。} {
        #class get_all kill;
        #showme GET_ALL_FULL;
    };
    #ac {你附近没有这样东西。} {
        #class get_all kill;
        #showme GET_ALL_DONE;
    };
    #class get_all close;
};

#al {picker.sell_all} {
    #var sell_all_item %0;
    #class sell_all open;
    #ti {sell_all} {
        sell ${sell_all_item};
    } {3};
    #ac {这样东西不值钱。} {
        #class sell_all kill;
        #showme SELL_ALL_DONE;
    };
    #ac {你要卖断什么物品？} {
        #class sell_all kill;
        #showme SELL_ALL_DONE;
    };
    #class sell_all close;
};
#class picker close;
