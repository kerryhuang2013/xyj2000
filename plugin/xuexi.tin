#class xuexi open;
#var xue_count 5;
#al {xue.start} {
    #var xue_jineng %1;
    show_to_quest 学习: ${xue_jineng};
    xue.setup;
    restart_xue;
    wd.start {
        xue.start ${xue_jineng};
    };
};

#al {xue.stop} {
    show_to_quest 学习完成;
    #unticker {xue};
    cancel_dazuo;
    #class xue.inner kill;
    wd.stop;
};

#al {xue.setup} {
    #class xue.inner open;
    #al {do_xue} {
        xue ${xue_jineng} from ${shifu} for ${xue_count};
    };
    #ac {和你切磋了一会儿} {
        xue.stop;
    };
    #ac {你的潜能不足！} {
        show_to_quest 潜能不足;
        xue.stop;
    };
    #ac {似乎有些心得。} {
        wd.kick;
    };
    #ac {你的法术修为还不够高深，无法学习} {
        show_to_quest spells 等级不够;
        xue.stop;
    };
    #al {start_xue} {
        #ti {xue} {
            do_xue;  
        } {1};
    };

    #al {restart_xue} {
        gt ${shifu_pos};
        on_there {
            start_xue;
        };  
    };
    
    #ac {你今天太累了，结果什么也没有学到。} {
        wd.kick;
        #unticker {xue};
        #delay {3} {
            #if {${nl_percent} > 30} {
                yun refresh;
                start_xue;
            };
            #else {
                chain chifan on_sleep restart_xue;
            };              
        };
    };
    #class xue.inner close;
};

#class xuexi close;
