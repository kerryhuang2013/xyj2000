#class xuexi open;
#alias {xue.start} {
    #var xue_jineng %1;
    show_to_quest 学习: ${xue_jineng};
    xue.setup;
    restart_xue;
    wd.start {
        xue.start ${xue_jineng};
    };
};

#alias {xue.stop} {
    show_to_quest 学习完成;
    #unticker {xue};
    cancel_dazuo;
    #class xue.inner kill;
    wd.stop;
};

#alias {xue.setup} {
    #class xue.inner open;
    #alias {do_xue} {
        xue ${xue_jineng} from ${shifu};
    };
    #action {你的潜能不足！} {
        show_to_quest 潜能不足;
        xue.stop;
    };
    #action {似乎有些心得。} {
        wd.kick;
    };
    #action {你的法术修为还不够高深，无法学习} {
        show_to_quest spells 等级不够;
        xue.stop;
    };
    #alias {start_xue} {
        #ticker {xue} {
            do_xue;  
        } {1};
    };

    #alias {restart_xue} {
        gt ${shifu_pos};
        on_there {
            start_xue;
        };  
    };
    
    #action {你今天太累了，结果什么也没有学到。} {
        wd.kick;
        #unticker {xue};
        #delay {1} {
            chain chifan do_dazuo on_sleep restart_xue;            
        };
    };
    #class xue.inner close;
};

#class xuexi close;
