#class lian open;
#var lian_count 10;
#alias {lian.start} {
    show_to_quest 练武;
    lian.setup;
    get_next_to_lian;
    #if {"${skill_to_lian}" != "nil"} {
        chain chifan start_lian;
        wd.start {lian.start};
    };
    #else {
        lian.stop;
    };
};

#alias {lian.stop} {
    stop_lian_ticker;
    #class lian.inner kill;
    show_to_quest 练武结束;
    wd.stop;
};

#alias {lian.setup} {
    #class lian.inner open;
    #var lian_index 2;
    #alias {get_next_to_lian} {
        #math lian_index ${lian_index}+1;
        #if {${lian_index} > ${skills_size}} {
            #var skill_to_lian nil;
            #return;
        };
        #list skills get ${lian_index} skill_to_lian;
        #var x @get_level_a{${skill_to_lian}};
        #var y @get_level_b{${skill_to_lian}};
        #if {${x} <= ${y} || ${x} ==0 || ${y} == 0} {
            get_next_to_lian;
        };
    };
    
    #alias {start_lian} {
        gt ${shifu_pos};
        on_there {
            start_lian_ticker;
        };
    };

    #alias {start_lian_ticker} {
        #ticker {lian} {
            lian ${skill_to_lian} ${lian_count};
        } {1};
    };
    
    #alias {stop_lian_ticker} {
        #unticker {lian};
    };

    #ticker {check_level} {
        #if {@get_level_a{${skill_to_lian}} <= @get_level_b{${skill_to_lian}}} {
            stop_lian_ticker;
            get_next_to_lian;
            #if {"${skill_to_lian}" != "nil"} {
                start_lian_ticker;
            };
            #else {
                lian.stop;
            };
        };        
    } {60};
    
    #action {必须空手} {
        unwield all;
    };

    #alias {on_low_qx} {
        #if {${nl_percent} > 30} {
            yun recover;
        };
        #else {
            stop_lian_ticker;
            #delay {lian_delay} {
                chain on_sleep chifan start_lian;                
            } {30};
        };  
    };

    #alias {on_low_js} {
        #if {${nl_percent} > 30} {
            yun refresh;
        };
        #else {
            stop_lian_ticker;
            chain on_sleep chifan start_lian;
        };  
    };

    #action {你的内力或气不够} {
        on_low_qx;
    };

    #action {你的体力太弱} {
        on_low_qx;
    };

    #action {你的气或内力不够} {
        on_low_qx;
    };

    #action {你的内力不够了} {
        dazuo ${dazuo_count};
    };
    
    #action {你的精神太差了，不能练} {
        on_low_js;
    };

    #action {你的精神无法集中} {
        on_low_js;
    };

    #action {你的%*进步了} {
        wd.kick;
    };
    #class lian.inner close;
};

#class lian close;
