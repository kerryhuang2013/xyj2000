#alias c chat;
#alias * chat*;
#alias r reply;

#alias {load} {
    #var v %1;
    #if {"${v}" == "spec"} {
        #read plugin/spec/${char_menpai}${dot}tin;
    };
    #else {
        #read plugin/%1.tin;
    };
};

#alias {unload} {
    #class %1 kill;
};

#alias {chifan} {
    #6 {drink cha};
};

#alias {lm} {
    map;
};

#alias {grep} {
    #grep %1;
};

#alias {le} {l east};
#alias {lw} {l west};
#alias {ls} {l south};
#alias {ln} {l north};

#alias {i} {
    #send {i %0};
    lock 总计;
};

#alias {sc} {
    #send {score %0};
    lock 内力修为;
};

#alias {hp} {
    #send {hp};
    lock 杀气：;
};

#alias {sk} {
    #send {skills %0};
    lock 你目前所掌握的技能;
};

#alias {jifa} {
    #send {jifa %0};
    lock 以下是你目前使用中的特殊技能;
};

#alias {lock} {
    #if {"%0" != ""} {
        #action {%0} {
            #action {^>} {
                #buffer lock;
                #unaction {^>}
            };
            #unaction {%0};
        };
    };
};

#alias {iall} {
    #echo {≡━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━≡};
    #send {i ring};
    #send {i necklace};
    #send {i spear};
    #send {i staff};
    #send {i blade};
    #send {i shield};
    #send {i sword};
    #send {i whip};
    #send {i head};
    #send {i hands};
    #send {i waist};
    #send {i armor};
    #send {i cloth};
    #send {i boots};
    #send {i wrists};
    #send {i surcoat};
    #action {你身上id为surcoat的东西有下面这些} {
        #delay {0.5} {
            #echo {≡━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━≡};
            #buffer lock;
        };
    };
};

#alias {heal.stop} {
    #unticker {heal};
};

#alias {heal.start} {
    #ticker {heal} {
        yun lifeheal %1;
        #edelay {2} {
            dazuo 100;
        };
    } {5};
};

#alias {on_hit} {
    #alias tmp_hit #cr;
    #alias tmp_hit %0;
    #action {你战胜了} {
        #unaction {你战胜了};
        #delay {2} {
            halt;
            tmp_hit;
        };
    };
};

#alias {on_dead} {
    #alias tmp_dead #cr;
    #alias tmp_dead %0;
    #action {死了} {
        #unaction {死了};
        #delay {2} {
            halt;
            tmp_dead;
        };
    } {1};
};

#alias {health.cancel} {
    #class health.inner kill;
    #alias tmp_health #cr;
    dz.cancel;
    tn.cancel;
};

#alias {on_health} {
    #alias tmp_health #cr;
    #alias tmp_health %0;
    #class health.inner open;
    #alias {check_qx} {
        #if {${eff_qx_percent} < 99} {
            #if {${qx_percent} < 50} {
                yun lifeheal ${char_id};
            };
            #else {
                yun heal;
            }
            #delay {0.5} {
                check_qx;
            };
        };
        #else {
            check_js;
        };
    };
    #alias {check_js} {
        #if {${eff_js_percent} < 99} {
            yun inspire;
            #delay {0.5} {
                check_js;
            };
        };
        #else {
            #class health.inner kill;
            re;
            chifan;
            #delay {0.5} {
                tmp_health;
            };
        };
    };

    #alias {check_all} {
        dz.full {
            tn.full {
                check_qx;
            };
        };
    };

    #action {已经受伤过重，经受不起你的真气震荡} {
        health.cancel;
        on_severe_wound;
     };

     #action {你已经受伤过重，只怕一运真气便有生命危险！} {
         health.cancel;
        on_severe_wound;
    };
    #class health.inner close;
    re;
    check_all;
};

#alias {on_sleep} {
    #alias tmp_wakeup #cr;
    #alias tmp_wakeup %0;
    #class on_wakeup.inner open;
    #action {翻来覆去无法入眠!} {
        #delay {sleep} {
            sleep;
        } {10};
    };
    #action {睡过一觉} {
        #delay {sleep} {
            sleep;
        } {10};
    };
    #action {你一觉醒来} {
        #class on_wakeup.inner kill;
        #delay {wakeup} {
            tmp_wakeup;
        } {3};
    };
    #class on_wakeup.inner close;
    gt ${gps_sleep};
    on_there {
        sleep;
    };
};

#alias {init_set} {
    set wimpy 20;
};

#var dot .;
#alias {log.save} {
    #system {cp debug${dot}${char_id}${dot}log ${char_id}${dot}log};
};

#alias {log.start} {
    #log overwrite debug${dot}${char_id}${dot}log;
    #debug all log;
    #var debug_info on;
    update_status;
};

#alias {log.stop} {
    #log off;
    #debug all off;
};

#alias {play} {
    #system {mplayer sound/%1 1>/dev/null 2>&1 &};
};

#alias {walk_to} {
    #var  direction %1;
    #var  action #cr;
    #alias {get_pos_done} {
        #list exits_list create ${exits_list};
        #if {@ismember{${direction};exits_list}} {
            @abbrev{${direction}};
        };
        #else {
            #foreach {${exits_list}[%*]} {exit} {
                #regexp {${exit}} {%*${direction}%*} {#var action ${exit}};
            };
            @abbrev{${action}};
        };
    };
    get_pos {
        get_pos_done;
    };
};

#macro {\e[1;3A} {#nop ];
walk_to north;
};
#macro {\e[1;3B} {#nop ];
walk_to south;
};
#macro {\e[1;3D} {#nop ];
walk_to west;
};
#macro {\e[1;3C} {#nop ];
walk_to east;
};

#alias {unbusy} {
    on_unbusy {%1; on_unbusy {%2 ;on_unbusy {%3 ;on_unbusy {%4 ;on_unbusy {%5 ;on_unbusy {%6 ;on_unbusy {%7 ;on_unbusy {%8 ;on_unbusy {%9 ;on_unbusy {%10 ;on_unbusy {%11 ;on_unbusy {%12;};};};};};};};};};};};};
};

#alias {chain} {
    on_unbusy {%1 {on_unbusy {%2 {on_unbusy {%3 {on_unbusy {%4 {on_unbusy {%5 {on_unbusy {%6 {on_unbusy {%7 {on_unbusy {%8 {on_unbusy {%9 {on_unbusy {%10 {on_unbusy {%11 {on_unbusy {%12;};};};};};};};};};};};};};};};};};};};};};};};
};

#alias {sell.all} {
    #alias tmp_sell #cr;
    #alias tmp_sell %1;
    #alias {sell_weapon} {
        on_there {
            sell all;
            #delay {3} {
                sell_other;
            };
        };
        gt rbz;
    };

    #alias {sell_other} {
        #class sell_other.inner open;
        #action {你身上没有puti zi} {
            #ticker {sell_other} {
                sell baicao dan;
            } {1};
        };
        #action {你身上没有baicao dan} {
            #class sell_other.inner kill;
            #unticker {sell_other};
            #delay {3} {
                tmp_sell;
            };
        };
        #class sell_other.inner close;
        on_there {
            #ticker {sell_other} {
                sell puti zi;
            } {1};
        };
        gt 扬州当铺;
    };
    sell_weapon;
};

#alias {repair} {
    #alias tmp_repair #cr;
    #alias tmp_repair %1;
    #if {${min_naijiu} < 350} {
        #action {抵达目的地} {
            #unaction {抵达目的地};
            e;ne;
            #delay {2} {
                #foreach {${my_weapons}[%*]} {weapon} {
                    fix ${weapon};
                };
                play repair.wav;
                #delay {3} {
                    sw;w;
                    #delay {3} {
                        tmp_repair;
                    };
                };
            };
        };
        gt ly;
    };
    #else {
        tmp_repair;
    };
};

#function {py} {
    INVOKE pinyin %0;
};

#alias {n} {north;look};
#alias {s} {south;look};
#alias {e} {east;look};
#alias {w} {west;look};
#alias {u} {up;look};
#alias {d} {down;look};
#alias {ne} {northeast;look};
#alias {sw} {southwest;look};
#alias {nw} {northwest;look};
#alias {se} {southeast;look};
#alias {nd} {northdown;look};
#alias {su} {southup;look};
#alias {nu} {northup;look};
#alias {sd} {southdown;look};
#alias {eu} {eastup;look};
#alias {wd} {westdown;look};
#alias {ed} {eastdown;look};
#alias {wu} {westup;look};

#list kaimen_actions create {open;push;pull;break};
#list kaimen_names create {door;gate;men;stone};
#alias {kaimen} {
    #gag 什么？;
    #foreach {${kaimen_actions}[%*]} {kaimen_action} {
        #foreach {${kaimen_names}[%*]} {kaimen_name} {
            #show ${kaimen_action} ${kaimen_name};
            ${kaimen_action} ${kaimen_name};
        };
    };
    #delay {1} {
        #ungag 什么？;
    };
};

#alias {on_echo} {
    #alias tmp_echo #cr;
    #alias tmp_echo %0;
    #action {^>} {
        #unaction {^>};
        tmp_echo;
    };
};

#alias {l} {
    #ungag {^$};
    #send {l %0};
    #delay {gag_delay} {
        #gag {^$};
    } {1.5};
    #action {^>} {
        #unaction {^>};
        #gag {^$};
    }
};

#macro {\eL} {
    show.reset;
};

#alias {INVOKE} {
    #script {file_name} {python3 -m bin.%0 ${char_id}};
    #var file_name ${file_name}[1];
    #if {"${file_name}" != ""} {
        #read ${file_name};
        #system {rm ${file_name}};
    };
};

#var pid 0;
#alias {check_heart_beat} {
    #delay {check_heart_beat_delay} {
        #if {${pid} != 0} {
            #system {kill -30 ${pid}};
            end_and_restart;
        };
    } {120};
};

#event {SESSION DEACTIVATED} {
    #if {"%0" == "pkuxkx"} {
        #if {${pid} != 0} {
            #system {kill -30 ${pid}};
            end_and_restart;
        };
    };
};

#alias {end_and_restart} {
    #end;
};

#alias {end} {
    #unevent {SESSION DEACTIVATED};
    unset_last_quest;
    #end;
};

#alias {quit_and_restart} {
    #class quit.inner.open;
    #action {你丢下} {
        #class quit.inner kill;
        #end;
    };
    #ticker {quit} {
        #send {quit};
    } {3};
    #class quit.inner.close;
    #send {quit};
};

#alias {quit} {
    #unevent {SESSION DEACTIVATED};
    unset_last_quest;
    quit_and_restart;
};

#alias {set_last_quest} {
    #var last_quest %1;
    xmpp.status ${last_quest};
    @setenv{last_quest;${last_quest}};
};

#alias {unset_last_quest} {
    #var last_quest nil;
    xmpp.status ${last_quest};
    @setenv{last_quest;nil};
};

#alias {restart_last_quest} {
    #var last_quest @getenv{last_quest};
    #if {"${last_quest}" != "nil"} {
        ${last_quest}${dot}start;
    };
};

#alias {notify} {
    play alert.wav;
    show_to_info %0;
    #nop desktop.notify %0;
    xmpp.notify %0;
};

#var busying 0;
#list unbusy_list create {};
#alias {do_unbusy} {
    #list unbusy_list size busy_list_size;
    #if {${busy_list_size} == 0} {
        #var busy_action nil;
    };
    #else {
        #list unbusy_list get ${busy_list_size} busy_action;
        #list unbusy_list delete ${busy_list_size};
    };
    #if {"${busy_action}" != "nil"} {
        #var busying 1;
        on_unbusy_inner {
            ${busy_action};
            do_unbusy;
        };
    };
    #else {
        #var busying 0;
    };
};

#alias {on_unbusy} {
    #list unbusy_list add {%0};
    #if {${busying} == 0} {
        do_unbusy;
    };
};

#gag {禁止练功};
#gag {你最少要花 20 点「气」才能练功。};
#alias {on_unbusy_inner} {
    #alias tmp_unbusy #cr;
    #alias tmp_unbusy %0;
    #class unbusy.inner open;
    #action {禁止练功} {
        #class unbusy.inner kill;
        #undelay {delay_unbusy};
        tmp_unbusy;
    };
    #action {你最少要花 20 点「气」才能练功。} {
        #class unbusy.inner kill;
        #undelay {delay_unbusy};
        tmp_unbusy;
    } {1};
    #ticker {checkbusy} {
        dazuo 1;
    } {0.5};
    #class unbusy.inner close;
    dazuo 1;
};

@mapcreate{menpai};
@mapset{menpai;朝廷;chaoting};

#alias {corpse} {
    get all from corpse;
};

#alias {on_server_reboot} {
    #showme INFO: 服务器重启!;
};

#alias {shifu} {
    gt $gps_shifu;
};

#alias walk {
    #alias tmp_walk_to #cr;
    #alias tmp_walk_to %1;

    #class on_walk open;
    #action {^%S - } {
        #class on_walk kill;
        #undelay walk_delay;
        #showme {ROOM_CHANGED};
    };
    #delay walk_delay {
        #class on_walk kill;
        #showme {ROOM_NOT_CHANGED};
    } {3};
    #class on_walk close;

    tmp_walk_to;
}

#alias get_bishuizhou {
    gt 南城客栈;
    on_there {
        buy jiudai from er;
    };
    #class bishuizhou open;
    #action {你撕开无字天书的背页，从里面小心翼翼地取出一张小纸片。} {
        #class bishuizhou kill;
    };
    #action {你向店小二买下一个桂花酒袋} {
        gt 袁氏草堂;
        on_there {
            give jiudai to yuan;
            #delay {1} {
                tear book;
            };
        };
    };
    #class bishuizhou close;
};

#alias get_pillow {
    gt 南城客栈;
    on_there {
        buy pillow;
    };
};
