#class lj open;
#var lj_stopped 1;
#alias {lj.start} {
    #var lj_stopped 0;
    wd.start {lj.start};
    lj.setup;
    lj.prepare;
};

#alias {lj.stop} {
    wd.stop;
    #var lj_stopped 1;
    #undelay {lj.delay};
    health.cancel;
    lj.teardown;
};

#alias {lj.prepare} {
    wd.kick;
    health {
        lj.q;
    };
};

#alias {lj.q} {
    wd.kick;
    gt lj;
    on_there {
        ask li about kill;
    };
};

#alias {lj.teardown} {
    #class lj.inner kill;
    cancel_prepare;
};

#alias {lj.setup} {
    #class lj.inner open;
    #action {慢慢地你终于又有了知觉．．．} {
        lj.prepare;
    };

    #action {你得到了%*和%*点潜能！} {
        wd.kick;
        show_to_quest 灭妖: 完成;
        show_to_info 完成: ${lj_area}, 获得 @ctd{%%2} 点潜能;
        #if {${lj_stopped} == 0} {
            lj.prepare;
        };
    };
    
    #action {看来该找机会逃跑了．．．} {
        lj.prepare;
    };

    #action {李靖对手中照妖镜一看，说：%*妖魔已除尽,不妨先去歇息。} {
        lj.wait;
    };
    
    #action {李靖说道：老夫不是派你去} {
        lj.wait;
    };

    #alias {lj.wait} {
        wd.kick;
        #delay {lj.delay} {
            ask li about kill;   
        } {20};
        show_to_quest 灭妖: 等待;
    };

    #action {找到目标} {
        wd.kick;
        follow @lower{${lj_target}};
    };

    #action {你决定开始跟随%*一起行动。} {
        kill @lower{${lj_target}};        
    };

    #action {落荒而逃了。} {
        follow @lower{${lj_target}};
    };
    #action {你灭妖有功} {
    };

    #action {GPS: 遍历完成} {
        wd.kick;
        show_to_info 没有找到怪: ${lj_area};
        #nop lj.prepare;
    };
    
    #alias {lj.bl} {
        wd.kick;
        bl.start {${lj_target}};
    };

    #action {李靖对你说道：近有%*(%*)在%*为非作歹，请速去降服！}{
        wd.kick;
        show_to_quest 灭妖: %%1 @ %%3;
        #var lj_target %%2;
        #var lj_area %%3;
        gt %%3;
        on_there {
            lj.bl;
        };
    };
    #class lj.inner close;    
};
#class lj close;
