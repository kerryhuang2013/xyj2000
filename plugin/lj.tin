#class lj open;
#var lj_stopped 1;
#al {lj.start} {
    #var lj_stopped 0;
    wd.start {lj.start};
    lj.setup;
    quest.start {
        lj.q;
    };
};

#al {lj.stop} {
    wd.stop;
    #var lj_stopped 1;
    #undelay {lj.delay};
    health.cancel;
    lj.teardown;
};

#al {lj.prepare} {
    wd.kick;
    quest.prepare {
        lj.q;
    };
};

#al {lj.q} {
    wd.kick;
    gt lj;
    on_there {
        ask li about kill;
    };
};

#al {lj.teardown} {
    #class lj.inner kill;
    cancel_prepare;
};

#al {lj.setup} {
    #class lj.inner open;
    #ac {慢慢地你终于又有了知觉．．．} {
        lj.prepare;
    };

    #ac {你得到了%*和%*点潜能！} {
        wd.kick;
        show_to_quest 灭妖: 完成;
        show_to_info 完成: ${lj_area}, 获得 @ctd{%%2} 点潜能;
        #if {${lj_stopped} == 0} {
            lj.prepare;
        };
    };
    
    #ac {看来该找机会逃跑了．．．} {
        health {
            lj.bl.bfs;
        };
    };

    #ac {这里没有 %*。} {
        #if {"${lj_target}" == "%%1"} {
            wd.kick;
            lj.bl.bfs;
        };
    };

    #ac {%*往%*落荒而逃了。} {
        #if {"%%1" == "${lj_target_zh}"} {
            #delay {1} {
                follow ${lj_target};
            };
        };
    };
    
    #ac {李靖对手中照妖镜一看，说：%*妖魔已除尽,不妨先去歇息。} {
        lj.wait;
    };
    
    #ac {李靖说道：老夫不是派你去} {
        lj.wait;
    };

    #al {lj.wait} {
        wd.kick;
        #delay {lj.delay} {
            ask li about kill;   
        } {30};
        show_to_quest 灭妖: 等待;
    };

    #ac {找到目标} {
        wd.kick;
        follow ${lj_target};
    };

    #ac {你决定开始跟随%*一起行动。} {
        kill ${lj_target};
    };
    
    #ac {GPS: 遍历完成} {
        wd.kick;
        show_to_info 没有找到怪: ${lj_area};
        #nop lj.prepare;
    };
    
    #al {lj.bl} {
        wd.kick;
        bl.start {${lj_real_target}};
    };

    #al {lj.bl.bfs} {
        wd.kick;
        bl.start {${lj_real_target}} bfs;
    };

    #ac {李靖对你说道：近有%*(%*)在%*为非作歹，请速去降服！}{
        wd.kick;
        show_to_quest 灭妖: %%1 @ %%3;
        #var lj_target_zh %%1;
        #var lj_real_target %%2;
        #var lj_target @lower{${lj_real_target}};
        #var lj_area %%3;
        gt %%3;
        on_there {
            lj.bl;
        };
    };
    #class lj.inner close;    
};
#class lj close;
