#class lj open;
#var lj_stopped 1;

#var lj_request_count 0;
#var lj_succ_count 0;
#var lj_qn_count 0;
#var lj_miss_count 0;

#al {lj.start} {
    #var lj_stopped 0;
    wd.start {lj.restart};
    lj.setup;
    quest.start {
        lj.q;
    };
};

#al {lj.restart} {
    after_launch {
        lj.start;
    };
};

#al {lj.status} {
    #math lj_succ_rate (${lj_succ_count}*100)/(${lj_request_count}+1);
    #math lj_miss_rate (${lj_miss_count}*100)/(${lj_request_count}+1);
    #echo {成功: ${lj_succ_count}/${lj_request_count} = ${lj_succ_rate}};
    #echo {没找到怪: ${lj_miss_count}/${lj_request_count} = ${lj_miss_rate}};
    #echo {获得潜能: ${lj_qn_count}};
};
#al {lj.stop} {
    wd.stop;
    #var lj_stopped 1;
    #undelay {lj.delay};
    health.cancel;
    lj.teardown;
};

#al {lj.prepare} {
    wd.kick;
    quest.prepare {
        lj.q;
    };
};

#al {lj.q} {
    wd.kick;
    gt lj;
    on_there {
        ask li about kill;
    };
};

#al {lj.teardown} {
    #class lj.inner kill;
    cancel_prepare;
};

#al {lj.setup} {
    #class lj.inner open;
    #ac {慢慢地你终于又有了知觉．．．} {
        lj.prepare;
    };

    #ac {你得到了%*点武学经验、%*天%*道行和%*点潜能！} {
        #math lj_succ_count ${lj_succ_count}+1;
        #math lj_qn_count ${lj_qn_count}+@ctd{%%4};
        wd.kick;
        show_to_quest 灭妖: 完成;
        show_to_info 完成: ${lj_area}, 获得 @ctd{%%4} 点潜能, @ctd{%%1} 点武学, @ctd{%%2} 天道行;
        #var lj_done 1;
        #if {${lj_stopped} == 0} {
            lj.prepare;
        };
    };
    
    #ac {看来该找机会逃跑了．．．} {
        health {
            lj.bl.bfs;
        };
    };

    #ac {这里没有 %*。} {
        #if {"${lj_target}" == "%%1" && ${lj_done} == 0} {
            wd.kick;
            lj.bl.bfs;
        };
    };

    #ac {%*往%*落荒而逃了。} {
        #if {"%%1" == "${lj_target_zh}"} {
            #delay {1} {
                follow ${lj_target};
            };
        };
    };

    #ac {你急急忙忙跑了过来，四处张望，却什么都没发现。} {
        follow ${lj_target};
    };
    
    #ac {李靖对手中照妖镜一看，说：%*妖魔已除尽,不妨先去歇息。} {
        lj.wait;
    };
    
    #ac {李靖说道：老夫不是派你去} {
        lj.wait;
    };

    #al {lj.wait} {
        wd.kick;
        #delay {lj.delay} {
            ask li about kill;   
        } {30};
    };

    #ac {找到目标} {
        wd.kick;
        follow ${lj_target};
    };

    #ac {你决定开始跟随%*一起行动。} {
        kill ${lj_target};
    };
    
    #ac {GPS: 遍历完成} {
        wd.kick;
        #math lj_miss_count ${lj_miss_count}+1;
        show_to_info 没有找到怪: ${lj_area};
        lj.prepare;
    };
    
    #al {lj.bl} {
        wd.kick;
        bl.start {${lj_real_target}};
    };

    #al {lj.bl.bfs} {
        wd.kick;
        bl.start {${lj_real_target}} bfs;
    };

    #ac {李靖对你说道：近有%*(%*)在%*为非作歹，请速去降服！}{
        #math lj_request_count ${lj_request_count}+1;
        wd.kick;
        #var lj_done 0;
        show_to_quest 灭妖: %%1 @ %%3;
        #var lj_target_zh %%1;
        #var lj_real_target %%2;
        #var lj_target @lower{${lj_real_target}};
        #var lj_area %%3;
        gt %%3;
        on_there {
            lj.bl;
        };
    };
    #class lj.inner close;    
};
#class lj close;
