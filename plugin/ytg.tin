#class ytg open;
#var ytg_stopped 1;
#al {ytg.start} {
    #var ytg_stopped 0;
    wd.start {ytg.start};
    ytg.setup;
    ytg.prepare;
};

#al {ytg.stop} {
    wd.stop;
    #var ytg_stopped 1;
    #undelay {ytg.delay};
    ytg.teardown;
};

#al {ytg.prepare} {
    wd.kick;
    prepare;
};

#al {ytg.q} {
    wd.kick;
    gt ytg;
    on_there {
        ask yuan about kill;
    };
};

#al {ytg.teardown} {
    #class ytg.inner kill;
    cancel_prepare;
};

#al {ytg.setup} {
    #class ytg.inner open;
    #var ytg_got_task 0;
    #ac {PREPARE_DONE} {
        ytg.q;
    };
    #ac {慢慢地你终于又有了知觉．．．} {
        ytg.prepare;
    };
    #ac {看来该找机会逃跑了．．．} {
        ytg.prepare;
    };
    #ac {袁天罡将手中桃木剑向四方一划，对你说道：} {
        #var ytg_got_task 1;
    };

    #ac {你向袁天罡打听有关『kill』的消息。} {
        #var ytg_got_task 0;
    };
    #ac {袁天罡说道：在下不是请您去收服} {
        ytg.wait;
    };

    #al {ytg.wait} {
        wd.kick;
        #delay {ytg.delay} {
            ask yuan about kill;   
        } {20};
        show_to_quest 灭妖: 等待;
    };
    
    #ac {袁天罡将手中桃木剑缓缓放下} {
        ytg.wait;
    };

    #ac {找到目标} {
        wd.kick;
        kill @lower{${ytg_target}};
    };

    #ac {你灭妖有功} {
        wd.kick;
        show_to_quest 灭妖: 完成;
        show_to_info 完成: ${ytg_area};
        
        #if {${ytg_stopped} == 0} {
            ytg.prepare;
        };
    };

    #ac {GPS: 遍历完成} {
        wd.kick;
        show_to_info 没有找到怪: ${ytg_area};
        ytg.prepare;
    };
    
    #al {ytg.bl} {
        wd.kick;
        bl.start {${ytg_target}};
    };

    #ac {　　近有%*(%*)在%*出没，为害不小，请速去收服！} {
        #if {${ytg_got_task} == 1} {
            wd.kick;
            show_to_quest 灭妖: %%1 @ %%3;
            #var ytg_target %%2;
            #var ytg_area %%3;
            gt %%3;
            on_there {
                ytg.bl;
            };
        };
    };
    #class ytg.inner close;    
};
#class ytg close;
