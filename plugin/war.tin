#class war open;

#alias {yun} {exert};

#alias {re} {
    yun recover;
    yun refresh;
};

#action {你对%*说道：%*领教%*高招！} {
    war;
};

#action {你对%*大喝一声：看招！} {
    war;
};

#action {看起来%1想杀死你} {
    war;
};

#action {你似乎十分疲惫，看来需要好好休息了} {re} {1};
#action {你气喘嘘嘘，看起来状况并不太好} {re} {1};
#action {你看起来已经力不从心了} {re} {1};

#alias {raw} {
    #var pfming 0;
    #unticker {fighting};
    @stackcreate{pfm};
    #class war.inner kill;
    #class attack.inner kill;
    #undelay {attack.delay};
    attack.end;
    #var enemy nil;
    #var enemy_menpai nil;
};

#alias {war.monitor} {
    #class war.inner open;

    #class war.inner close;
};

#alias {war} {
    war.monitor;
    attack;
};

#alias {attack.end} {#cr};
#alias {attack} {#cr};

#var enemy nil;
#alias {perform} {
    #if {"${enemy}" == "nil"} {
        #send {perform %0};
    };
    #else {
        #send {perform %0 ${enemy}};
    };
};

#gag clear: done;
#alias {clear} {
    kill %0;
    #class clear.inner open;
    #action {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        raw;
        #showme clear: done;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};

#alias {on_cleared} {
    #alias tmp_on_cleared %0;
    #action {clear: done} {
        #unaction {clear: done};
        #delay {3} {
            tmp_on_cleared;
        };
    };
};

#alias {kill} {
    #send kill %0;
    war;
};

#alias {killall} {
    #send killall %0;
    war;
};

#alias {hit} {
    #send hit %0;
    war;
};

#alias {fight} {
    #send fight %0;
    war;
};

#var pfming 0;
#alias {pfm.inner} {
    #var pfm_action @stackpoll{pfm};
    #if {"${pfm_action}" != "nil"} {
        #var pfming 1;
        on_unbusy {
            ${pfm_action};
            #var pfming 0;
            pfm.inner;
        };
    };
    #else {
        #var pfming 0;
    };
};

@stackcreate{pfm};
#alias {pfm} {
    #if {@stackismember{pfm;%1} == 0} {
        @stackpush{pfm;%1};
        #if {${pfming} == 0} {
            pfm.inner;
        };
    };
};

#alias {cancel_prepare} {
    #class prepare kill;
    #undelay {recover_delay};
};

#alias {prepare} {
    #class prepare open;
    #action {EQUIP_DONE} {
        save_qn;
    };
    #action {SAVE_QN_DONE} {
        chifan;
    };
    #action {CHIFAN_DONE} {
        recover;
    };
    #action {RECOVER_DONE} {
        #class prepare kill;
        #showme PREPARE_DONE;
    };
    #class prepare close;
    equip;
};

#alias {recover} {
    #if {${qx_percent} > 90} {
        #showme RECOVER_DONE;
    };
    #else {
        #delay {recover_delay} {
            recover;
        } {10};
    };
};

#alias {equip} {
    #alias {do_equip} {
        gt 281;
        on_there {
            drop shield;
            drop tianyi;
            drop shoes;
            drop bonnet;
            drop yupei;
            drop ring;
            drop hammer;
            drop pifeng;
            drop armor;
            yao pifeng;
            yao shield;
            yao armor;
            yao tianyi;
            yao shoes;
            yao bonnet;
            yao yupei;
            yao ring;
            yao hammer;
            remove all;
            wear all;
            wield hammer;
            #class equip kill;
            #showme EQUIP_DONE;            
        };        
    };
    #class equip open;
    #action {ITEM_FOUND} {
        #class equip kill;
        #showme EQUIP_DONE;
    };
    #action {ITEM_NOT_FOUND} {
        do_equip;
    };
    #class equip close;
    checkitem {meihua hammer};
};

#alias {check_gongfu} {
    #class check_gongfu open;
    #list skills create  {force;spells;cuff;strike;staff;sword;parry;blade;spear;dodge;hammer};
    #list skills size skill_size;
    #list level_a create {};
    #list level_b create {};
    #list level_a clear;
    #list level_b clear;
    #loop 1 $skill_size tmp {#list level_b add 0;#list level_a add 0};
    #var skill_index 1;
    #list skills get $skill_index skill;

    #action {  %S (%*)%s： %S%s有效等级：} {
        #var basic_skill_en %%2;
        #var special_skill_zh %%4;
        #list special_skills find ${special_skill_zh} tmp;
        #if {${tmp} != 0} {
            #list special_skills_level get ${tmp} tmp_level;
            record_level_b ${basic_skill_en} ${tmp_level};
            #if {"${basic_skill_en}" == "force"} {
                #math nl_limit ${nl_limit}+${tmp_level}*10;
            };
            #if {"${basic_skill_en}" == "spells"} {
                #math fl_limit ${fl_limit}+${tmp_level}*10;
            };
        };
    } {2};

    #action {┃  %S (%S)%s- %S%s%d/} {
        #echo match;
        #var basic_skill_zh %%1;
        #var basic_skill_en %%2;
        #var basic_skill_level %%6;
        record_level_a  ${basic_skill_en} ${basic_skill_level};
        #if {"${basic_skill_en}" == "force"} {
            #math nl_limit ${basic_skill_level}*5;
        };
        #if {"${basic_skill_en}" == "spells"} {
            #math fl_limit ${basic_skill_level}*5;
        };
    } {3};

    #action {┃□%S (%S)%s - %S%s%d/} {
        #echo match;
        #var special_skill_zh %%1;
        #var special_skill_en %%2;
        #var special_skill_level %%6;
        #list special_skills add ${special_skill_zh};
        #list special_skills_level add ${special_skill_level};
    } {2};

    #alias {get_level_a} {
        #var skill_level_a 0;
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_a get $tmp_index tmp;
            #var skill_level_a $tmp;
        };
    };

    #alias {get_level_b} {
        #var skill_level_b 0;
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_b get $tmp_index tmp;
            #var skill_level_b $tmp;
        };
    };

    #alias {record_level_a} {
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_a set $tmp_index %%2;
        };
    };

    #alias {record_level_b} {
        #list skills find %%1 tmp_index;
        #if {$tmp_index != 0} {
            #list level_b set $tmp_index %%2;
        };
    };

    #class check_gongfu close;
    #send {skills};
    #send {jifa};
};
#class war close;
