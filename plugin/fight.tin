#class war open;

#alias {re} {
    #alias re_action #cr;
    #alias re_action %0;
    yun recover;
    yun refresh;
    re_action;
};

#action {看起来%1想杀死你} {
    attack.prepare;
    war;
};

#action {你似乎十分疲惫，看来需要好好休息了} {re} {1};
#action {你气喘嘘嘘，看起来状况并不太好} {re} {1};
#action {你看起来已经力不从心了} {re} {1};

#alias {raw} {
    #var pfming 0;
    #unticker {fighting};
    @stackcreate{pfm};
    #class war.monior kill;
    #class attack.inner kill;
    #var enemy nil;
    #var enemy_menpai nil;
};

#alias {war.monitor} {
    #class war.monitor open;
    #action {你全身放松} {
        raw;
    };
    #ticker {fighting} {
        yun heal;
    } {5};
    #class war.monitor close;
};

#alias {war} {
    war.monitor;
    attack;
};

#alias {attack} {#cr};
#alias {attack.prepare} {#cr};

#var enemy nil;
#alias {perform} {
    #if {"${enemy}" == "nil"} {
        #send {perform %0};
    };
    #else {
        #send {perform %0 ${enemy}};
    };
};

#gag clear: done;
#alias {clear} {
    kill %0;
    #class clear.inner open;
    #action {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        raw;
        #showme clear: done;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};

#alias {on_cleared} {
    #alias tmp_on_cleared %0;
    #action {clear: done} {
        #unaction {clear: done};
        #delay {3} {
            tmp_on_cleared;
        };
    };
};

#alias {kill} {
    attack.prepare;
    #send kill %0;
    war;
};

#alias {killall} {
    attack.prepare;
    #send killall %0;
    war;
};

#alias {hit} {
    attack.prepare;
    #send hit %0;
    war;
};

#alias {fight} {
    attack.prepare;
    #send fight %0;
    war;
};

#gag 战斗中运功疗伤？找死吗？;
#gag 你上一个动作还没有完成;
#gag 你上一个动作还没有完成，不能念咒文;
#gag 你现在不能用魔法;

#var casting 0;
#alias {do_cast} {
    #class do_cast open;
    #alias {retry_cast} {
        #delay {do_cast_delay} {
            ${cast_action};
        } {0.5};
        #delay {cast_delay} {
            #class do_cast kill;
            cast.next;
        } {1};  
    };
    #action {( 你上一个动作还没有完成，不能念咒文。)} {
        retry_cast;
    };
    #action {你现在不能用魔法！} {
        retry_cast;
    };
    #class do_cast close;
    retry_cast;
};

#alias {cast.next} {
    #var cast_action @stackpoll{cast};

    #if {"${cast_action}" != "nil"} {
        #var casting 1;
        do_cast;
    };
    #else {
        #var casting 0;
    };
};

@stackcreate{cast};
#alias {cst} {
    #if {@stackismember{cast;%1} == 0} {
        @stackpush{cast;%1};
        #if {${casting} == 0} {
            cast.next;
        };
    };
};

#alias {cancel_prepare} {
    #class prepare kill;
    #undelay {recover_delay};
};

#alias {prepare} {
    #class prepare open;
    #action {EQUIP_DONE} {
        cun_qn;
    };
    #action {CUN_QN_DONE} {
        chifan;
    };
    #action {CHIFAN_DONE} {
        recover;
    };
    #action {RECOVER_DONE} {
        #class prepare kill;
        #showme PREPARE_DONE;
    };
    #class prepare close;
    equip;
};

#alias {recover} {
    #if {${qx_percent} > 90} {
        #showme RECOVER_DONE;
    };
    #else {
        #delay {recover_delay} {
            recover;
        } {10};
    };
};

#alias {equip} {
    #alias equip_action #cr;
    #alias equip_action %1;
    #alias {equip_all}  {
        remove all;
        unwield all;
        wear all;
        wield all;
    };
    #alias {do_equip} {
        gt 281;
        on_there {
            drop shield;
            drop tianyi;
            drop shoes;
            drop bonnet;
            drop yupei;
            drop ring;
            drop ${weapon_to_wield};
            drop pifeng;
            drop armor;
            drop fist;
            yao pifeng;
            yao shield;
            yao armor;
            yao tianyi;
            yao shoes;
            yao bonnet;
            yao yupei;
            yao ring;
            yao fist;
            yao ${weapon_to_wield};
            equip_all;
            #class equip kill;
            #showme EQUIP_DONE;
            equip_action;
        };        
    };
    #class equip open;
    #action {ITEM_FOUND} {
        equip_all;
        #class equip kill;
        #showme EQUIP_DONE;
        equip_action;
    };
    #action {ITEM_NOT_FOUND} {
        do_equip;
    };
    #class equip close;
    checkitem {quan tao};
};

#alias {health.cancel} {
    #class health.inner kill;
    #alias tmp_health #cr;
    dz.full.cancel;
    ms.full.cancel;
    qx.full.cancel;
    js.full.cancel;
};

#alias {qx.full.cancel} {
    #alias {check_qx} {#cr};
};

#alias {js.full.cancel} {
    #alias {check_js} {#cr};
};

#alias {qx.full} {
    #alias qx_full_action #cr;
    #alias qx_full_action %0;
    #alias {check_qx} {
        #if {${eff_qx_percent} < 70} {
            buy 5 yao from boss;
            eat 5 yao;
            #delay {2} {
                check_qx;
            };
        };
        #else {
            qx_full_action;
        };
    };
    #if {${eff_qx_percent} >= 70} {
        qx_full_action;
        #return;
    };
    gt 回春药铺;
    on_there {
        check_qx;
    };
};

#alias {js.full} {
    #alias js_full_action #cr;
    #alias js_full_action %0;
    #alias {check_js} {
        #if {${eff_js_percent} < 70} {
            buy 5 dan from boss;
            eat 5 dan;
            #delay {2} {
                check_js;
            };
        };
        #else {
            js_full_action;
        };
    };
    #if {${eff_js_percent} >= 70} {
        js_full_action;
        #return;
    };
    gt 回春药铺;
    on_there {
        check_js;
    };    
};

#alias {health} {
    #alias tmp_health #cr;
    #alias tmp_health %0;
    chain chifan dz.full ms.full qx.full js.full re tmp_health;
};
#class war close;
