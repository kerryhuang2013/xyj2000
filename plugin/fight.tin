#class fight open;

#al {re} {
    #al re_action #cr;
    #al re_action %0;
    yun recover;
    yun refresh;
    re_action;
};

#ac {看起来%1想杀死你} {
    attack.prepare;
    war;
};

#ac {你似乎十分疲惫，看来需要好好休息了} {re} {1};
#ac {你气喘嘘嘘，看起来状况并不太好} {re} {1};
#ac {你看起来已经力不从心了} {re} {1};
#ac {你受了相当重的伤，只怕会有生命危险。} {re} {1};
#ac {你气息粗重，动作开始散乱，看来所受的伤着实不轻。} {re} {1};
#ac {你伤重之下已经难以支撑，眼看就要倒在地上。} {re} {1};

#al {raw} {
    #var pfming 0;
    #unticker {fighting};
    #unticker {re};
    @stackcreate{pfm};
    #class war.monior kill;
    #class attack.inner kill;
    #var enemy nil;
    #var enemy_menpai nil;
};

#al {war.monitor} {
    #class war.monitor open;
    #ac {你气血圆满，不需要疗伤。} {
        raw;
    };
    #ac {你全身放松} {
        raw;
    };
    #ac {你已经受伤过重，只怕一运真气便有生命危险！} {
        raw;
    };
    #ticker {fighting} {
        yun heal;
    } {5};
    #class war.monitor close;
};

#al {war} {
    health.cancel;
    war.monitor;
    #class attach.inner open;
    #ti {re} {
        re;
    } {3};
    #class attach.inner close;
    attack;
};

#var enemy nil;
#al {perform} {
    #if {"${enemy}" == "nil"} {
        #send {perform %0};
    };
    #else {
        #send {perform %0 ${enemy}};
    };
};

#al {clear} {
    kill %0;
    #class clear.inner open;
    #ac {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        raw;
        #showme CLEAR_DONE;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};

#al {on_cleared} {
    #al tmp_on_cleared %0;
    #ac {CLEAR_DONE} {
        #unaction {CLEAR_DONE};
        #delay {3} {
            tmp_on_cleared;
        };
    };
};

#al {kill} {
    attack.prepare;
    #send kill %0;
    war;
};

#al {killall} {
    attack.prepare;
    #send killall %0;
    war;
};

#al {hit} {
    attack.prepare;
    #send hit %0;
    war;
};

#al {fight} {
    attack.prepare;
    #send fight %0;
    war;
};

#gag 战斗中运功疗伤？找死吗？;
#gag 你上一个动作还没有完成;
#gag 你上一个动作还没有完成，不能念咒文;
#gag 你现在不能用魔法;

#var casting 0;
#al {do_cast} {
    #class do_cast open;
    #al {retry_cast} {
        #delay {do_cast_delay} {
            ${cast_action};
        } {0.5};
        #delay {cast_delay} {
            #class do_cast kill;
            cast.next;
        } {1};  
    };
    #ac {( 你上一个动作还没有完成，不能念咒文。)} {
        retry_cast;
    };
    #ac {你现在不能用魔法！} {
        retry_cast;
    };
    #class do_cast close;
    retry_cast;
};

#al {cast.next} {
    #var cast_action @stackpoll{cast};

    #if {"${cast_action}" != "nil"} {
        #var casting 1;
        do_cast;
    };
    #else {
        #var casting 0;
    };
};

@stackcreate{cast};
#al {cst} {
    #if {@stackismember{cast;%1} == 0} {
        @stackpush{cast;%1};
        #if {${casting} == 0} {
            cast.next;
        };
    };
};

#al {cancel_prepare} {
    #class prepare kill;
    #undelay {recover_delay};
};

#al {prepare} {
    #class prepare open;
    #ac {EQUIP_DONE} {
        cun_qn;
    };
    #ac {CUN_QN_DONE} {
        chifan;
    };
    #ac {CHIFAN_DONE} {
        recover;
    };
    #ac {RECOVER_DONE} {
        #class prepare kill;
        #showme PREPARE_DONE;
    };
    #class prepare close;
    equip;
};

#al {recover} {
    #if {${qx_percent} > 90} {
        #showme RECOVER_DONE;
    };
    #else {
        #delay {recover_delay} {
            recover;
        } {10};
    };
};

#al {equip} {
    #al equip_action #cr;
    #al equip_action %0;
    #al {equip_all}  {
        remove linen;
        wear all;
        wield all;
    };

    #al {do_equip} {
        gt 281;
        on_there {
            drop shield;
            drop tianyi;
            drop shoes;
            drop bonnet;
            drop yupei;
            drop ring;
            drop ${weapon_to_wield};
            drop pifeng;
            drop armor;
            drop fist;
            yao pifeng;
            yao shield;
            yao armor;
            yao tianyi;
            yao shoes;
            yao bonnet;
            yao yupei;
            yao ring;
            yao fist;
            #5 yao ${weapon_to_wield};
            equip_all;
            equip_done;
        };        
    };

    #al {equip_done} {
        #class equip kill;
        #delay {1} {
            #send {southwest};
            #showme EQUIP_DONE;
            equip_action;
        };
    };
    
    #class equip open;
    #ac { 兵器伤害力：[%*]%s盔甲保护力：[%*]} {
        #if {%%1 <= 100 || %%3 <= 100} {
            do_equip;
        };
        #else {
            equip_done;
        };
    };
    #class equip close;
    equip_all;
    #send {score};
};

#al {health.cancel} {
    #class health.inner kill;
    #al tmp_health #cr;
    dz.full.cancel;
    ms.full.cancel;
    qx.full.cancel;
    js.full.cancel;
};

#al {qx.full.cancel} {
    #al {check_qx} {#cr};
};

#al {js.full.cancel} {
    #al {check_js} {#cr};
};

#al {qx.full} {
    #al qx_full_action #cr;
    #al qx_full_action %0;
    #al {check_qx} {
        #if {${eff_qx_percent} < 70} {
            eat 5 yao;
            #delay {2} {
                check_qx;
            };
        };
        #else {
            qx_full_action;
        };
    };
    check_qx;
};

#al {js.full} {
    #al js_full_action #cr;
    #al js_full_action %0;
    #al {check_js} {
        #if {${eff_js_percent} < 70} {
            eat 5 dan;
            #delay {2} {
                check_js;
            };
        };
        #else {
            js_full_action;
        };
    };
    check_js;
};

#al {health} {
    #al tmp_health #cr;
    #al tmp_health %0;
    chain qx.full js.full chifan dz.full ms.full re tmp_health;
};

#ac {你所使用的外功中没有这种功能。} {
    wield ${weapon_to_wield};
};

#al {attack} {#cr};
#al {attack.prepare} {#cr};
#al {do_reborn} {#cr};

#al {reborn} {
    #al reborn_action #cr;
    #al reborn_action %0;
    
    #al {do_reborn_if_needed} {
        #if {@get_level_b{force} == 0} {
            do_reborn;
            reborn_action;
        };
        #else {
            reborn_action;
        };
    };
    check_gongfu {
        do_reborn_if_needed;
    };
};

#class fight close;
