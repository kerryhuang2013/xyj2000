#class xiulian open
#tab dz.start;
#tab dz.stop;

#var dazuo_count 20;
#var dz_total_count 0;
#var ms_total_count 0;

#var health_percent 190;

#al {ms.full.cancel} {
    #class ms.full.inner kill;
    #unticker {ms};
};

#al {ms.full} {
    #al tmp_ms #cr;
    #al tmp_ms %0;
    #class ms.full.inner open;
    #ac {你现在神智不清,不能再想入非非了。} {
        yun refresh;
    };
    #class ms.full.inner close;
    #if {${fl_percent} >= ${health_percent}} {
        #class ms.full.inner kill;
        tmp_ms;
    };
    #else {
        #delay {ms_delay} {
            #ti {ms} {
                #if {${fl_percent} < ${health_percent}} {
                    mingsi ${dazuo_count};
                };
                #else {
                    #unticker {ms};
                    #class ms.full.inner kill;
                    #delay {ms_delay} {
                        tmp_ms;
                        #showme ms.full;
                    } {1};
                };
            } {3};
        }{1};
    };
};

#al {dz.full.cancel} {
    #class dz.full.inner kill;
    #unticker {dz};
};

#al {dz.full} {
    #al tmp_dz #cr;
    #al tmp_dz %0;
    #class dz.full.inner open;
    #ac {你现在精不够} {
        heal 1;
    };
    #class dz.full.inner close;
    #if {${nl_percent} >= ${health_percent}} {
        #class dz.full.inner kill;
        tmp_dz;
    };
    #else {
        #delay {dz_delay} {
            #ti {dz} {
                #if {${nl_percent} < ${health_percent}} {
                    yun recover; dazuo ${dazuo_count};
                };
                #else {
                    #unticker {dz};
                    #class dz.full.inner kill;
                    #delay {dz_delay} {
                        tmp_dz;
                    } {1};
                };
            } {3};
        } {1};
    };
};

#al {cancel_dazuo} {
    stop_dz;
    #class dazuo kill;
};

#al {do_dazuo} {
    #al dazuo_action #cr;
    #al dazuo_action %0;
    #if {${limit_nl} < ${max_nl} + 1} {
        #delay {1} {
            #showme DAZUO_DONE;            
            dazuo_action;
        };

        #return;
    };

    #class dazuo open;
    #al {start_dazuo} {
        #ti {dz} {
            dazuo ${dazuo_count};
        } {3};
    };

    #al {stop_dazuo} {
        #unticker {dz};
    };

    #ac {你现在的气太少了} {
        stop_dz;
        #class dazuo kill;
        #showme DAZUO_DONE;
        dazuo_action;
    };

    #al {stop_dz} {
        stop_dazuo;
    };
    
    #al {start_dz} {
        gt ${shifu_pos};
        on_there {
            start_dazuo;
        };
    };
    
    #ac {你的内力增强} {
        #math dz_total_count ${dz_total_count}+1;
        show_to_info force +${dz_total_count};
    };
    
    #al {check_and_start_dz} {
        stop_dz;
        chifan {
            start_dz;
        };
    };
    #class dazuo close;

    check_and_start_dz;
};

#al {dz.start} {
    #class dz.inner open;
    #ac {DAZUO_DONE} {
        wd.kick;
        #if {${limit_nl} < ${max_nl} + 1} {
            dz.stop;
            #return;
        };
        on_sleep {
            do_dazuo;
        };
    };

    #class dz.inner close;
    show_to_quest 打坐;
    do_dazuo;
    wd.start {
        dz.start;
    };
};

#al {dz.stop} {
    cancel_dazuo;
    #class dz.inner kill;
    show_to_quest 打坐完毕;
    wd.stop;
};

#al {cancel_mingsi} {
    stop_ms;
    #class mingsi kill;
};

#al {do_mingsi} {
    #if {${limit_fl} < ${max_fl} + 1} {
        #delay {1} {
            #showme MINGSI_DONE;            
        };
        #return;
    };
    #class mingsi open;
    #al {start_ms} {
        gt ${shifu_pos};
        on_there {
            start_mingsi;
        };
    };
    #al {start_mingsi} {
        #ti {mingsi}{
            mingsi 20
        } {3};
    };
    #ac {你的法力增强} {
        #math ms_total_count ${ms_total_count}+1;
        show_to_info spells +${ms_total_count};
    };

    #ac {你现在神智不清,不能再想入非非了。} {
        stop_ms;
        #class mingsi kill;
        #showme MINGSI_DONE;
    };
    #al {stop_ms} {
        #unticker {mingsi};
    };

    #al {check_and_start_ms} {
        stop_ms;
        chifan {
            start_ms;
        };
    };
    #class mingsi close;
    
    check_and_start_ms;
};

#al {ms.start} {
    #class ms.inner open;
    #ac {MINGSI_DONE} {
        wd.kick;
        #if {${limit_fl} < ${max_fl} + 1} {
            ms.stop;
            #return;
        };
        on_sleep {
            do_mingsi;            
        };
    };
    #class ms.inner close;
    show_to_quest 冥思;
    do_mingsi;
    wd.start{
        ms.start;
    };
};

#al {ms.stop} {
    wd.stop;
    cancel_mingsi;
    #class ms.inner kill;
    show_to_quest 冥思完毕;
};

#al {xl.start} {
    #class xiulian.inner open;
    #ac {DAZUO_DONE} {
        wd.kick;
        do_mingsi;
    };

    #ac {MINGSI_DONE} {
        wd.kick;
        #if {${max_nl} + 1 < ${limit_nl}} {
            on_sleep {
                do_dazuo;
            };  
        };
        #elseif {${max_fl} + 1 < ${limit_fl}} {
            on_sleep {
                do_mingsi;
            };
        };
        #else {
            xl.stop;
        };
    };
    #class xiulian.inner close;
    wd.start {
        xl.start;
    };
    do_dazuo;
    show_to_quest 修炼;
};

#al {xl.stop} {
    #class xiulian.inner kill;
    wd.stop;
    cancel_mingsi;
    cancel_dazuo;
    show_to_quest 修炼结束;
};
#class xiulian close
