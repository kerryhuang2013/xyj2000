#class xiulian open
#tab dz.start;
#tab dz.stop;

#var dazuo_count 20;
#var dz_total_count 0;
#var ms_total_count 0;
#var dz_max 95;

#alias {dz.cancel} {
    #class dz.full.inner kill;
    #unticker {dz};
};

#alias {dz.full} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #class dz.full.inner close;
    #delay {dz_delay} {
        #ticker {dz} {
            #if {${nl_percent} < ${dz_max}} {
                yun recover; dazuo ${dazuo_count};
            };
            #else {
                #unticker {dz};
                #class dz.full.inner kill;
                #delay {dz_delay} {
                    tmp_dz;
                    #showme dz.full;
                } {3};
            };
        } {1};
    }{1};
};

#alias {cancel_dazuo} {
    stop_dz;
    #class dazuo kill;
};

#alias {do_dazuo} {
    #alias dazuo_action #cr;
    #alias dazuo_action %1;
    #if {${limit_nl} < ${max_nl} + 1} {
        #delay {1} {
            #showme DAZUO_DONE;            
            dazuo_action;
        };

        #return;
    };

    #class dazuo open;
    #alias {start_dazuo} {
        #ticker {dz} {
            dazuo ${dazuo_count};
        } {3};
    };

    #alias {stop_dazuo} {
        #unticker {dz};
    };

    #action {你现在的气太少了} {
        stop_dz;
        #class dazuo kill;
        #showme DAZUO_DONE;
        dazuo_action;
    };

    #alias {stop_dz} {
        stop_dazuo;
    };
    
    #alias {start_dz} {
        gt ${shifu_pos};
        on_there {
            start_dazuo;
        };
    };
    
    #action {你的内力增强} {
        #math dz_total_count ${dz_total_count}+1;
        show_to_info force +${dz_total_count};
    };
    
    #alias {check_and_start_dz} {
        stop_dz;
        chifan;
        #action {CHIFAN_DONE} {
            start_dz;
        };
    };
    #class dazuo close;

    check_and_start_dz;
};

#alias {dz.start} {
    #class dz.inner open;
    #action {DAZUO_DONE} {
        wd.kick;
        #if {${limit_nl} < ${max_nl} + 1} {
            dz.stop;
            #return;
        };
        on_sleep {
            do_dazuo;
        };
    };

    #class dz.inner close;
    show_to_quest 打坐;
    do_dazuo;
    wd.start {
        dz.start;
    };
};

#alias {dz.stop} {
    cancel_dazuo;
    #class dz.inner kill;
    show_to_quest 打坐完毕;
    wd.stop;
};

#alias {cancel_mingsi} {
    stop_ms;
    #class mingsi kill;
};

#alias {do_mingsi} {
    #if {${limit_fl} < ${max_fl} + 1} {
        #delay {1} {
            #showme MINGSI_DONE;            
        };
        #return;
    };
    #class mingsi open;
    #alias {start_ms} {
        gt ${shifu_pos};
        on_there {
            start_mingsi;
        };
    };
    #alias {start_mingsi} {
        #ticker {mingsi}{
            mingsi 20
        } {3};
    };
    #action {你的法力增强} {
        #math ms_total_count ${ms_total_count}+1;
        show_to_info spells +${ms_total_count};
    };

    #action {你现在神智不清,不能再想入非非了。} {
        stop_ms;
        #class mingsi kill;
        #showme MINGSI_DONE;
    };
    #alias {stop_ms} {
        #unticker {mingsi};
    };

    #alias {check_and_start_ms} {
        stop_ms;
        chifan;
        #action {CHIFAN_DONE} {
            start_ms;
        };
    };
    #class mingsi close;
    
    check_and_start_ms;
};

#alias {ms.start} {
    #class ms.inner open;
    #action {MINGSI_DONE} {
        wd.kick;
        #if {${limit_fl} < ${max_fl} + 1} {
            ms.stop;
            #return;
        };
        on_sleep {
            do_mingsi;            
        };
    };
    #class ms.inner close;
    show_to_quest 冥思;
    do_mingsi;
    wd.start{
        ms.start;
    };
};

#alias {ms.stop} {
    wd.stop;
    cancel_mingsi;
    #class ms.inner kill;
    show_to_quest 冥思完毕;
};

#alias {xl.start} {
    #class xiulian.inner open;
    #action {DAZUO_DONE} {
        wd.kick;
        do_mingsi;
    };

    #action {MINGSI_DONE} {
        wd.kick;
        #if {${limit_fl} < ${max_fl} + 1} {
            xl.stop;
        };
        #if {${limit_nl} < ${max_nl} + 1} {
            on_sleep {
                do_mingsi;
            };  
        };
        #else {
            on_sleep {
                do_dazuo;
            };
        }
    };
    #class xiulian.inner close;
    wd.start {
        xl.start;
    };
    do_dazuo;
    show_to_quest 修炼;
};

#alias {xl.stop} {
    #class xiulian.inner kill;
    wd.stop;
    cancel_mingsi;
    cancel_dazuo;
    show_to_quest 修炼结束;
};
#class xiulian close
