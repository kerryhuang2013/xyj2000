#class xiulian open
#tab dz.start;
#tab dz.stop;

#var dazuo_count 20;
#var dz_total_count 0;
#var dz_max 95;

#alias {dz.cancel} {
    #class dz.full.inner kill;
    #unticker {dz};
};

#alias {dz.full} {
    #alias tmp_dz #cr;
    #alias tmp_dz %1;
    #class dz.full.inner open;
    #action {你现在精不够} {
        heal 1;
    };
    #class dz.full.inner close;
    #delay {dz_delay} {
        #ticker {dz} {
            #if {${nl_percent} < ${dz_max}} {
                yun recover; dazuo ${dazuo_count};
            };
            #else {
                #unticker {dz};
                #class dz.full.inner kill;
                #delay {dz_delay} {
                    tmp_dz;
                    #showme dz.full;
                } {3};
            };
        } {1};
    }{1};
};

#alias {cancel_dazuo} {
    stop_dz;
    #class dazuo kill;
};

#alias {do_dazuo} {
    #class dazuo open;
    #alias {start_ticker} {
        #ticker {dz} {
            dazuo ${dazuo_count};
        } {3};
    };

    #alias {stop_ticker} {
        #unticker {dz};
    };

    #action {你现在的气太少了} {
        stop_dz;
        #class dazuo kill;
        #showme DAZUO_DONE;
    };

    #alias {stop_dz} {
        stop_ticker;
    };
    
    #alias {start_dz} {
        gt ${shifu_pos};
        on_there {
            start_ticker;
        };
    };
    
    #action {你的内力增强} {
        #math dz_total_count ${dz_total_count}+1;
        show_to_info force +${dz_total_count};
    };
    
    #alias {check_and_start_dz} {
        stop_dz;
        chifan;
        #action {CHIFAN_DONE} {
            start_dz;
        };
    };
    #class dazuo close;
    check_and_start_dz;
};

#alias {dz.start} {
    #class dz.inner open;
    #action {DAZUO_DONE} {
        on_sleep {
            do_dazuo;
        };
    };
    #action {似乎内力修为已经遇到了瓶颈} {
        dz.stop;
    };

    #class dz.inner close;
    show_to_quest 打坐;
    do_dazuo;
};

#alias {dz.stop} {
    cancel_dazuo;
    #class dz.inner kill;
    show_to_quest 打坐完毕;
};

#alias {cancel_mingsi} {
    stop_ms;
    #class mingsi kill;
};
#alias {do_mingsi} {
    #class mingsi open;
    #alias {start_ms} {
        gt ${shifu_pos};
        on_there {
            start_ticker;
        };
    };
    #alias {start_ticker} {
        #ticker {mingsi}{
            mingsi 20
        } {3};
    };
    #action {你现在神智不清,不能再想入非非了。} {
        stop_ms;
        #class mingsi kill;
        #showme MINGSI_DONE;
    };
    #alias {stop_ms} {
        #unticker {mingsi};
    };

    #alias {check_and_start_ms} {
        stop_ms;
        chifan;
        #action {CHIFAN_DONE} {
            start_ms;
        };
    };
    #class mingsi close;
    
    check_and_start_ms;
};

#alias {ms.start} {
    #class ms.inner open;
    #action {MINGSI_DONE} {
        on_sleep {
            do_mingsi;            
        };
    };
    #class ms.inner close;
    show_to_quest 冥思;
    do_mingsi;
};

#alias {ms.stop} {
    cancel_mingsi;
    #class ms.inner kill;
    show_to_quest 冥思完毕;
};
#class xiulian close
